--> I) Base case
open NSLPK .
-- arbitrary values
  op n1 : -> Nonce .
  op q1 : -> Prin .
-- check
  red inv2(init,n1,q1) .
close

--> II) Induction cases
--> 1. send1(s,p1,p2)
--> p2 = intr
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
-- assumptions
  eq p2 = intr .
-- check
  red inv2(s,n1,q1) implies inv2(send1(s,p1,p2),n1,q1) .
close
--> ~(p2 = intr), n1 = n(intr,p2,rand(s))
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
-- assumptions
  eq (p2 = intr) = false .
  eq n1 = n(intr,p2,rand(s)) .
-- check
  red inv2(s,n1,q1) implies inv2(send1(s,p1,p2),n1,q1) .
close
--> ~(p2 = intr), ~(n1 = n(intr,p2,rand(s))), n1 = n(p1,p2,rand(s))
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
-- assumptions
  eq (p2 = intr) = false .
  eq (n1 = n(intr,p2,rand(s))) = false .
  eq n1 = n(p1,p2,rand(s)) .
-- check
  red inv2(s,n1,q1) implies inv2(send1(s,p1,p2),n1,q1) .
close
--> ~(p2 = intr), ~(n1 = n(intr,p2,rand(s))), ~(n1 = n(p1,p2,rand(s)))
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
-- assumptions
  eq (p2 = intr) = false .
  eq (n1 = n(intr,p2,rand(s))) = false .
  eq (n1 = n(p1,p2,rand(s))) = false .
-- check
  red inv2(s,n1,q1) implies inv2(send1(s,p1,p2),n1,q1) .
close

--> 2. send2(s,p1,p2,m)
--> enc1(p1,m,p2) \in nw(s), intr = p2, n1 = n(p1,p2,rand(s))
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq enc1(p1,m,p2) \in nw(s) = true .
  eq intr = p2 .
  eq n1 = n(p1,p2,rand(s)) .
-- check
  red inv2(s,n1,q1) implies inv2(send2(s,p1,p2,m),n1,q1) .
close
--> enc1(p1,m,p2) \in nw(s), intr = p2, ~(n1 = n(p1,p2,rand(s))), n1 = m
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq enc1(p1,m,p2) \in nw(s) = true .
  eq intr = p2 .
  eq (n1 = n(p1,p2,rand(s))) = false .
  eq n1 = m .
-- check
  red inv2(s,n1,q1) implies inv2(send2(s,p1,p2,m),n1,q1) .
close
--> enc1(p1,m,p2) \in nw(s), intr = p2, ~(n1 = n(p1,p2,rand(s))), ~(n1 = m)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq enc1(p1,m,p2) \in nw(s) = true .
  eq intr = p2 .
  eq (n1 = n(p1,p2,rand(s))) = false .
  eq (n1 = m) = false .
-- check
  red inv2(s,n1,q1) implies inv2(send2(s,p1,p2,m),n1,q1) .
close
--> enc1(p1,m,p2) \in nw(s), ~(intr = p2)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq enc1(p1,m,p2) \in nw(s) = true .
  eq (intr = p2) = false .
-- check
  red inv2(s,n1,q1) implies inv2(send2(s,p1,p2,m),n1,q1) .
close
--> ~enc1(p1,m,p2) \in nw(s)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq enc1(p1,m,p2) \in nw(s) = false .
-- check
  red inv2(s,n1,q1) implies inv2(send2(s,p1,p2,m),n1,q1) .
close

--> 3. send3(s,p1,p2,m1,m2)
--> enc2(p1,m1,m2,p2) \in nw(s), enc1(p2,m1,p1) \in nw(s), intr = p2, n1 = m2
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  ops m1 m2 : -> Nonce .
-- assumptions
  eq enc2(p1,m1,m2,p2) \in nw(s) = true .
  eq enc1(p2,m1,p1) \in nw(s) = true .
  eq intr = p2 .
  eq n1 = m2 .
-- check
  red inv2(s,n1,q1) implies inv2(send3(s,p1,p2,m1,m2),n1,q1) .
close
--> enc2(p1,m1,m2,p2) \in nw(s), enc1(p2,m1,p1) \in nw(s), intr = p2, ~(n1 = m2)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  ops m1 m2 : -> Nonce .
-- assumptions
  eq enc2(p1,m1,m2,p2) \in nw(s) = true .
  eq enc1(p2,m1,p1) \in nw(s) = true .
  eq intr = p2 .
  eq (n1 = m2) = false .
-- check
  red inv2(s,n1,q1) implies inv2(send3(s,p1,p2,m1,m2),n1,q1) .
close
--> enc2(p1,m1,m2,p2) \in nw(s), enc1(p2,m1,p1) \in nw(s), ~(intr = p2)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  ops m1 m2 : -> Nonce .
-- assumptions
  eq enc2(p1,m1,m2,p2) \in nw(s) = true .
  eq enc1(p2,m1,p1) \in nw(s) = true .
  eq (intr = p2) = false .
-- check
  red inv2(s,n1,q1) implies inv2(send3(s,p1,p2,m1,m2),n1,q1) .
close
--> enc2(p1,m1,m2,p2) \in nw(s), ~enc1(p2,m1,p1) \in nw(s)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  ops m1 m2 : -> Nonce .
-- assumptions
  eq enc2(p1,m1,m2,p2) \in nw(s) = true .
  eq enc1(p2,m1,p1) \in nw(s) = false .
-- check
  red inv2(s,n1,q1) implies inv2(send3(s,p1,p2,m1,m2),n1,q1) .
close
--> ~enc2(p1,m1,m2,p2) \in nw(s)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  ops m1 m2 : -> Nonce .
-- assumptions
  eq enc2(p1,m1,m2,p2) \in nw(s) = false .
-- check
  red inv2(s,n1,q1) implies inv2(send3(s,p1,p2,m1,m2),n1,q1) .
close

--> 4. fake1(s,p1,p2,m)
--> m \in nonces(s), q1 = p2, n1 = m, p1 = intr
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq m \in nonces(s) = true .
  eq q1 = p2 .
  eq n1 = m .
  eq p1 = intr .
-- check
  red inv2(s,n1,q1) implies inv2(fake1(s,p1,p2,m),n1,q1) .
close
--> m \in nonces(s), q1 = p2, n1 = m, ~(p1 = intr)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq m \in nonces(s) = true .
  eq q1 = p2 .
  eq n1 = m .
  eq (p1 = intr) = false .
-- check
  red inv2(s,n1,q1) implies inv2(fake1(s,p1,p2,m),n1,q1) .
close
--> m \in nonces(s), q1 = p2, ~(n1 = m)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq m \in nonces(s) = true .
  eq q1 = p2 .
  eq (n1 = m) = false .
-- check
  red inv2(s,n1,q1) implies inv2(fake1(s,p1,p2,m),n1,q1) .
close
--> m \in nonces(s), ~(q1 = p2)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq m \in nonces(s) = true .
  eq (q1 = p2) = false .
-- check
  red inv2(s,n1,q1) implies inv2(fake1(s,p1,p2,m),n1,q1) .
close
--> ~(m \in nonces(s))
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq m \in nonces(s) = false .
-- check
  red inv2(s,n1,q1) implies inv2(fake1(s,p1,p2,m),n1,q1) .
close

--> 5. fake2(s,p1,p2,m1,m2)
--> m1 \in nonces(s), m2 \in nonces(s)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  ops m1 m2 : -> Nonce .
-- assumptions
  eq m1 \in nonces(s) = true .
  eq m2 \in nonces(s) = true .
-- check
  red inv2(s,n1,q1) implies inv2(fake2(s,p1,p2,m1,m2),n1,q1) .
close
--> m1 \in nonces(s), ~m2 \in nonces(s)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  ops m1 m2 : -> Nonce .
-- assumptions
  eq m1 \in nonces(s) = true .
  eq m2 \in nonces(s) = false .
-- check
  red inv2(s,n1,q1) implies inv2(fake2(s,p1,p2,m1,m2),n1,q1) .
close
--> ~m1 \in nonces(s)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 p2 q1 : -> Prin .
  ops m1 m2 : -> Nonce .
-- assumptions
  eq m1 \in nonces(s) = false .
-- check
  red inv2(s,n1,q1) implies inv2(fake2(s,p1,p2,m1,m2),n1,q1) .
close

--> 6. fake3(s,p1,m)
--> m \in nonces(s)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq m \in nonces(s) = true .
-- check
  red inv2(s,n1,q1) implies inv2(fake3(s,p1,m),n1,q1) .
close
--> ~m \in nonces(s)
open NSLPK .
-- arbitrary values
  op s : -> Sys .
  op n1 : -> Nonce .
  ops p1 q1 : -> Prin .
  op m : -> Nonce .
-- assumptions
  eq m \in nonces(s) = false .
-- check
  red inv2(s,n1,q1) implies inv2(fake3(s,p1,m),n1,q1) .
close

--> Q.E.D.
